
/***************************************************************************************************
 * Copyright Â© All Contributors. See LICENSE and AUTHORS in the root directory for details.
 **************************************************************************************************/

buildscript {
    ext.versions = [
        kotlin: '1.6.21',
        dokka: '1.5.0',
        ical4j: '3.1.2',
        // latest Apache Commons versions that don't require Java 8 (Android 7)
        commonsIO: '2.6'
    ]

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.2.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${versions.dokka}"
    }
}

repositories {
    google()
    mavenCentral()
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.dokka'

android {
    compileSdkVersion 32
    buildToolsVersion '32.0.0'

    defaultConfig {
        minSdkVersion 21        // Android 5.0
        targetSdkVersion 32     // Android 12

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "version_ical4j", "\"${versions.ical4j}\""
    }

    compileOptions {
        // ical4j >= 3.x uses the Java 8 Time API
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
        useIR = true
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES', 'META-INF/LICENSE', 'META-INF/*.md']
        }
    }
    lint {
        disable 'AllowBackup', 'InvalidPackage'
    }

    sourceSets {
        main.java.srcDirs = [ "src/main/java", "opentasks-contract/src/main/java" ]
    }

    // TODO currently doesn't work with dokka 1.5.0, but dokka 0.10.1 doesn't work with new gradle
    /*dokka.configuration {
        sourceLink {
            url = "https://gitlab.com/bitfireAT/ical4android/tree/master/"
            lineSuffix = "#L"
        }
        jdkVersion = 8
    }*/
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}"
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    api("org.mnode.ical4j:ical4j:${versions.ical4j}") {
        exclude module: 'javax.mail'
        exclude group: 'org.codehaus.groovy', module: 'groovy'
        exclude group: 'org.codehaus.groovy', module: 'groovy-dateutil'
    }
    // ical4j requires newer Apache Commons libraries, which require Java8. Force latest Java7 versions.
    // noinspection GradleDependency
    api("org.apache.commons:commons-collections4:4.2") { force = true }
    // noinspection GradleDependency
    api("org.apache.commons:commons-lang3:3.8.1") { force = true }

    // ical4j requires JavaMail API (e.g. for EMAIL parameter)
    implementation 'com.sun.mail:android-mail:1.6.7'
    // noinspection GradleDependency
    implementation "commons-io:commons-io:${versions.commonsIO}"

    implementation 'org.slf4j:slf4j-jdk14:1.7.32'
    implementation 'androidx.core:core-ktx:1.7.0'

    androidTestImplementation 'androidx.test:core:1.4.0'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    testImplementation 'junit:junit:4.13.2'
}